
function(enableH5Part exename)
    if (SPH_EXA_WITH_H5PART)
        target_compile_definitions(${exename} PUBLIC SPH_EXA_HAVE_H5PART)
        target_include_directories(${exename} PUBLIC ${PROJECT_SOURCE_DIR}/extern/h5part)
        # necessary because the HDF5_INLUCDE_DIRS is empty on CRAY and HIP-compilation is done without the cray-wrapper
        target_include_directories(${exename} PUBLIC $ENV{HDF5_DIR}/include)
        target_link_libraries(${exename} PRIVATE H5Part ${HDF5_LIBRARIES})
    endif()
endfunction()

function(enableH5ZZFP exename)
    if (SPH_EXA_WITH_HDF5)
        set(H5ZZFP_DIR "/home/appcell/unibas/zfpbuild")
        find_package(HDF5 REQUIRED)
        if (HDF5_FOUND)
            set(H5Z_ZFP_USE_STATIC_LIBS ON)
            target_compile_definitions(${exename} PUBLIC SPH_EXA_HAVE_HDF5)
            if(HDF5_IS_PARALLEL)
                find_package(MPI REQUIRED)
                target_compile_definitions(${exename} PUBLIC H5PART_PARALLEL_IO)
                target_include_directories(${exename} PRIVATE ${MPI_C_INCLUDE_DIRS})
                target_link_libraries(${exename} PRIVATE ${MPI_C_LIBRARIES})
            else()
                message("HDF5_IS_PARALLEL not found, Parallel HDF5 IO will not be enabled")
            endif()
            target_include_directories(${exename} PUBLIC ${HDF5_INCLUDE_DIRS} ${H5ZZFP_DIR}/include)
            # Here H5Z-ZFP is used as a library
            target_link_libraries(${exename} PUBLIC ${HDF5_LIBRARIES} ${H5ZZFP_DIR}/lib/libh5zzfp.a)
        else()
            message(STATUS "No HDF5 support")
            set(SPH_EXA_WITH_HDF5 OFF)
        endif ()
    endif()
endfunction()

function(enableInSituViz exename)
  if(INSITU STREQUAL "Catalyst")
    target_include_directories(${exename} PRIVATE ..)
    target_compile_definitions(${exename}
            PRIVATE
            "PARAVIEW_IMPL_DIR=\"${ParaView_CATALYST_DIR}\""
            SPH_EXA_USE_CATALYST2)
    target_link_libraries(${exename} PRIVATE catalyst::catalyst)
  elseif(INSITU STREQUAL "Ascent")
    target_include_directories(${exename} PRIVATE ..)
    target_compile_definitions(${exename}
            PRIVATE
            SPH_EXA_USE_ASCENT)
    target_link_libraries(${exename} PRIVATE ascent::ascent_mpi)
  endif()
endfunction()

function(enableGpuDirect exename)
    if (GPU_DIRECT)
        target_compile_definitions(${exename} PRIVATE USE_GPU_DIRECT)
    endif()
endfunction()

function(enableGrackle exename)
    if (SPH_EXA_WITH_GRACKLE)
        target_compile_definitions(${exename} PUBLIC SPH_EXA_HAVE_GRACKLE)
        target_compile_definitions(${exename} PUBLIC CONFIG_BFLOAT_8)
        target_link_libraries(${exename} PRIVATE grackle)
        target_link_libraries(${exename} PRIVATE cooler)
    endif ()
endfunction()

set(exename sphexa)
add_executable(${exename} sphexa.cpp)
target_include_directories(${exename} PRIVATE ${SPH_EXA_INCLUDE_DIRS})
target_link_libraries(${exename} PRIVATE OpenMP::OpenMP_CXX ${MPI_CXX_LIBRARIES})
target_include_directories(${exename} PRIVATE ${PROJECT_SOURCE_DIR}/physics/cooling/include/)

enableH5Part(${exename})
enableH5ZZFP(${exename})
enableInSituViz(${exename})
enableGrackle(${exename})
install(TARGETS ${exename} RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})

if (CMAKE_CUDA_COMPILER)
    add_executable(${exename}-cuda sphexa.cpp)
    target_include_directories(${exename}-cuda PRIVATE ${SPH_EXA_INCLUDE_DIRS})
    target_include_directories(${exename}-cuda PUBLIC ${CMAKE_BINARY_DIR}/main/src)
    target_compile_definitions(${exename}-cuda PRIVATE USE_CUDA)
    target_link_libraries(${exename}-cuda PRIVATE cstone_gpu ryoanji sph_gpu observables_gpu OpenMP::OpenMP_CXX
            ${MPI_CXX_LIBRARIES} CUDA::cudart)
    enableH5Part(${exename}-cuda)
    enableInSituViz(${exename}-cuda)
    enableGpuDirect(${exename}-cuda)
    enableGrackle(${exename}-cuda)
    install(TARGETS ${exename}-cuda RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})

elseif (CMAKE_HIP_COMPILER)
    add_executable(${exename}-hip sphexa.cpp)
    target_include_directories(${exename}-hip PRIVATE ${SPH_EXA_INCLUDE_DIRS})
    target_compile_definitions(${exename}-hip PRIVATE USE_CUDA THRUST_DEVICE_SYSTEM=THRUST_DEVICE_SYSTEM_HIP)
    target_link_libraries(${exename}-hip PRIVATE cstone_gpu ryoanji sph_gpu observables_gpu OpenMP::OpenMP_CXX
            ${MPI_CXX_LIBRARIES} hip::host)
    set_target_properties(${exename}-hip PROPERTIES LINKER_LANGUAGE CXX)
    enableH5Part(${exename}-hip)
    enableInSituViz(${exename}-hip)
    enableGpuDirect(${exename}-hip)
    enableGrackle(${exename}-hip)
    install(TARGETS ${exename}-hip RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR})
endif ()
